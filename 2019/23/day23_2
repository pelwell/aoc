#!/usr/bin/perl

use strict;
use integer;
use Time::HiRes qw(usleep);

my $trace = 0;

my @input;
my @output;

my $sum = 0;

$/ = undef; # slurp

while ($ARGV[0] =~ /^-/)
{
	my $arg = shift @ARGV;
	if ($arg eq '-t')
	{
		$trace = 1;
	}
	elsif ($arg eq '-i')
	{
		push @input, shift @ARGV;
	}
}

my $program = <>;
$program =~ s/\s+//g;

my @program = split(",", $program);

my @cpus;
my %id_busy;

for (my $id = 0; $id < 50; $id++)
{
	$cpus[$id] = make_cpu($id, \@program, \&net_rx, \&net_tx);
	$cpus[$id]->{'rx_queue'} = [ $id ];
	$cpus[$id]->{'tx_queue'} = [];
	$id_busy{$id} = 1;
}

my ($nat_x, $nat_y, $last_nat_y, $answer);

while (!defined $answer)
{
	for (my $id = 0; $id < 50; $id++)
	{
		step_cpu($cpus[$id]);
		last if (defined $answer);
	}
	if (!%id_busy)
	{
		printf("[%d, %d]\n", $nat_x, $nat_y);
		net_send(0, $nat_x, $nat_y);
		if ($nat_y == $last_nat_y)
		{
			$answer = $nat_y;
		}
		$last_nat_y = $nat_y;
	}
}

printf("Answer: %d\n", $answer);

exit(0);

sub net_rx
{
	my ($cpu) = @_;
	if (!@{$cpu->{'rx_queue'}})
	{
		delete $id_busy{$cpu->{'id'}};
		return -1;
	}
	
	return shift @{$cpu->{'rx_queue'}};
}

sub net_tx
{
	my ($cpu, $val) = @_;
	push @{$cpu->{'tx_queue'}}, $val;
	if (@{$cpu->{'tx_queue'}} == 3)
	{
		my ($dst, $x, $y) = splice(@{$cpu->{'tx_queue'}}, 0, 3);
		printf("%d->%d: %d, %d\n", $cpu->{'id'}, $dst, $x, $y);
		if ($dst == 255)
		{
			$nat_x = $x;
			$nat_y = $y;
		}
		else
		{
			net_send($dst, $x, $y);
		}
	}
}

sub net_send
{
	my ($dst, $x, $y) = @_;
	push @{$cpus[$dst]->{'rx_queue'}}, $x, $y;
	$id_busy{$dst} = 1;
}

####

sub make_cpu
{
	my ($id, $program, $input, $output) = @_;
	return {
		'id'=>$id,
		'mem'=>[@{$program}],
		'input'=>$input,
		'output'=>$output,
		'pc'=>0,
		'relbase'=>0,
	};
}

sub run_cpu
{
	my ($cpu) = @_;
	my $ret = -1;
	printf("[%s resuming]\n", $cpu->{'id'}) if ($trace);

	while ($ret == -1)
	{
		$ret = step_cpu($cpu);
	}

	printf("[ %s halting ]\n", $cpu->{'id'}) if ($trace);

	return $ret;
}

sub step_cpu
{
	my ($cpu) = @_;
	my $opc = opcode($cpu);
	printf("%d: opc %d\n", $cpu->{'pc'}, $opc) if ($trace);
	if ($opc == 1)
	{
		# $1 + $2 -> $3
		store($cpu, 3, param($cpu, 1) + param($cpu, 2));
		$cpu->{'pc'} += 4;
	}
	elsif ($opc == 2)
	{
		# $1 * $2 -> $3
		store($cpu, 3, param($cpu, 1) * param($cpu, 2));
		$cpu->{'pc'} += 4;
	}
	elsif ($opc == 3)
	{
		# input -> $1
		my $inp = input($cpu);
		if (!defined $inp)
		{
			printf("%s stalling for input\n", $cpu->{'id'}) if ($trace);
			return 0;
		}

		store($cpu, 1, $inp);
		$cpu->{'pc'} += 2;
	}
	elsif ($opc == 4)
	{
		# $1 -> output
		output($cpu, param($cpu, 1));
		$cpu->{'pc'} += 2;
	}
	elsif ($opc == 5)
	{
		# if $1, pc -> $2
		if (param($cpu, 1))
		{
			$cpu->{'pc'} = param($cpu, 2);
			print(" jump true -> $cpu->{'pc'}\n") if ($trace);
		}
		else
		{
			$cpu->{'pc'} += 3;
		}
	}
	elsif ($opc == 6)
	{
		# if !$1, pc -> $2
		if (!param($cpu, 1))
		{
			$cpu->{'pc'} = param($cpu, 2);
			print(" jump false -> $cpu->{'pc'}\n") if ($trace);
		}
		else
		{
			$cpu->{'pc'} += 3;
		}
	}
	elsif ($opc == 7)
	{
		# $3 = $1 < $2
		store($cpu, 3, (param($cpu, 1) < param($cpu, 2)) ? 1 : 0);
		$cpu->{'pc'} += 4;
	}
	elsif ($opc == 8)
	{
		# $3 = $1 == $2
		store($cpu, 3, (param($cpu, 1) == param($cpu, 2)) ? 1 : 0);
		$cpu->{'pc'} += 4;
	}
	elsif ($opc == 9)
	{
		# relbase += $1
		$cpu->{'relbase'} += param($cpu, 1);
		$cpu->{'pc'} += 2;
	}
	elsif ($opc == 99)
	{
		print("Program terminated at $cpu->{'pc'}\n") if ($trace);
		return 1;
	}
	else
	{
		print("Invalid opcode $opc at position $cpu->{'pc'}\n");
		return 1;
	}

	return -1;
}

sub opcode
{
	my ($cpu) = @_;
	return $cpu->{'mem'}->[$cpu->{'pc'}] % 100;
}

sub param_mode
{
	my ($cpu, $num) = @_;
	return ($cpu->{'mem'}->[$cpu->{'pc'}] / (10 ** ($num + 1))) % 10;
}

sub param
{
	my ($cpu, $num) = @_;

	my $param = $cpu->{'mem'}->[$cpu->{'pc'} + $num];

	my $mode = param_mode($cpu, $num);
	if ($mode == 0)
	{
		my $p = $cpu->{'mem'}->[$param];
		printf(" param %d = [%d] = %d\n", $num, $param, $p) if ($trace);
		return $p;
	}
	elsif ($mode == 2)
	{
		my $p = $cpu->{'mem'}->[$cpu->{'relbase'} + $param];
		printf(" param %d = [%d + %d] = %d\n", $num, $cpu->{'relbase'}, $param, $p) if ($trace);
		return $p;
	}
	printf(" param %d = %d\n", $num, $param) if ($trace);
	return $param;
}

sub store
{
	my ($cpu, $num, $val) = @_;

	my $mode = param_mode($cpu, $num);
	my $param = $cpu->{'mem'}->[$cpu->{'pc'} + $num];
	my $addr;
	if ($mode == 0)
	{
		$addr = $param;
		printf(" store %d -> [%d]\n", $val, $param) if ($trace);
	}
	elsif ($mode == 2)
	{
		$addr = $cpu->{'relbase'} + $param;
		printf(" store %d -> [%d + %d]\n", $val, $cpu->{'relbase'}, $param) if ($trace);
	}
	else
	{
		die "* Illegal parameter mode at $cpu->{'pc'}\n";
	}

	$cpu->{'mem'}->[$addr] = $val;
}

sub input
{
	my ($cpu) = @_;
	my $input = $cpu->{'input'};
	my $val;
	if (ref($input) eq 'ARRAY')
	{
		$val = shift @$input;
	}
	elsif (ref($input) eq 'CODE')
	{
		$val = $input->($cpu);
	}

	if (defined $val)
	{
		printf(" [ input %d ]\n", $val) if ($trace);
	}
	return $val;
}

sub output
{
	my ($cpu, $val) = @_;
	my $output = $cpu->{'output'};
	printf(" [ output %s ]\n", $val) if ($trace);
	if (ref($output) eq 'ARRAY')
	{
		push @$output, $val;
	}
	elsif (ref($output) eq 'CODE')
	{
		$output->($cpu, $val);
	}
}
